namespace com.linkedin.metadata.search.features

import com.linkedin.common.Urn

@Aspect = {
  "name": "usageFeatures"
}
record UsageFeatures {

  /**
   * Usage count in the last 30 days
   * This field is for Dashboard usage
   */
  @SearchScore = {
    "modifier": "LOG"
  }
  @Searchable = {
    "fieldName": "viewCountLast30DaysFeature",
    "fieldType": "COUNT"
  }
  viewCountLast30Days: optional long

  /**
   * View count total 
   * This field is for Dashboard usage
   */
  @Searchable = {
    "fieldName": "viewCountFeature",
    "fieldType": "COUNT"
  }
  viewCountTotal: optional long

  /**
   * Percentile rank of the dashboard views based on the the last 30 days view count on a platform
   */
  viewCountPercentileLast30Days: optional int

  /**
   * Deprecated - Use queryCountLast30Days instead. Usage count in the last 30 days
   */
  @SearchScore = {
    "modifier": "LOG"
  }
  @Searchable = {
    "fieldName": "usageCountLast30DaysFeature",
    "fieldType": "COUNT"
  }
  usageCountLast30Days: optional long

  /**
   * Dataset Query count in the last 30 days
   */
  @Searchable = {
    "fieldName": "queryCountLast30DaysFeature",
    "fieldType": "COUNT"
  }
  queryCountLast30Days: optional long

  /**
   * Unique user count in the last 30 days
   */
  @SearchScore = {
    "modifier": "LOG"
  }
  @Searchable = {
    "fieldName": "uniqueUserCountLast30DaysFeature",
    "fieldType": "COUNT"
  }
  uniqueUserCountLast30Days: optional long

  /**
   * Write (insert/update) count in the last 30 days
   */
  @Searchable = {
    "fieldName": "writeCountLast30DaysFeature",
    "fieldType": "COUNT"
  }
  writeCountLast30Days: optional long

  /**
   * Percentile rank of the dataset based on the the last 30 days usage count on a platform
   */
  queryCountPercentileLast30Days: optional int
  
  /**
   * Overall rank of the dataset based on the the last 30 days usage count on a platform
   * It returns one plus the number of rows proceeding or equals to the current row in the ordering. 
   */
  queryCountRankLast30Days: optional long

  /**
   * Percentile rank of the dataset based on the the last 30 days of select queries by unique users on a platform
   */
  uniqueUserPercentileLast30Days: optional int
  
  /**
   * Overall rank of the dataset based on the the last 30 days of select queries by unique users on a platform
   * It returns one plus the number of rows proceeding or equals to the current row in the ordering. 
   */
  uniqueUserRankLast30Days: optional long

  /**
   * Percentile rank of the dataset based on the the last 30 days insert/update operation count on a platform
   */
  writeCountPercentileLast30Days: optional int
  
  /**
   * Overall rank of the dataset based on the the last 30 days insert/update operation count on a platform
   * It returns one plus the number of rows proceeding or equals to the current row in the ordering. 
   */
  writeCountRankLast30Days: optional long

  /**
   * Ranked list of the top 10 users in past 30 days. 
   * It is ranked by highest usage descending.
   */
  topUsersLast30Days: optional array[Urn]

  /**
   * Deprecated - use StorageFeatures.pdl instead. The percentile of size in bytes relative to other assets from the data plaform instance.
   */
  sizeInBytesPercentile: optional long

  /**
   * Deprecated - use StorageFeatures.pdl instead. The percentile of size in bytes relative to other assets from the data plaform instance.
   */
  sizeInBytesRank: optional long

  /**
   * Deprecated - use StorageFeatures.pdl instead. The percentile of row count relative to other assets from the data platform instance.
   */
  rowCountPercentile: optional int
}
